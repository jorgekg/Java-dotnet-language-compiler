#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
M: [A-Z]
m: [a-z]
w: [M,m]
N: [0-9]
F: \\s | \\n |\\t
linha: \\\\(.)*\n
#Tokens
palavra		: {m} ( {m} | {M}{m} )*
id		: _((({m}|{M}({m}|{N}))({m}|{M}{m}|{M}{N}|{N})*({M})?)|{M})
cnumerica	: {N}+(\.{N}+)?
ccaractere	: '{F}'
cliteral		: \"([^\n\"\\])*\"

and		= palavra : "and"
array		= palavra : "array"
do		= palavra : "do"
exit		= palavra : "exit"
false		= palavra : "false"
if		= palavra : "if"
ifFalseDo 	= palavra : "ifFalseDo"
ifTrueDo 	= palavra : "ifTrueDo"
literal		= palavra : "literal"
logical		= palavra : "logical"
number		= palavra : "number"
not		= palavra : "not"
or		= palavra : "or"
read		= palavra : "read"
true		= palavra : "true"
write		= palavra : "write"

":"
","
"["
"]"
"{"
"}"
"("
")"
":="
"+"
"-"
"*"
"/"
"="
"!="
"<"
"<="
">"
">="

: [\s \n \t]
: {linha}
#NonTerminals
<program>
<var_declare>
<type>
<simple>
<array>
<id_list>
<id_list1>
<comands>
<comands1>
<comand>
<var_attrib>
<col>
<read>
<id_read_list>
<id_read_list1>
<write>
<exp_list>
<exp_list1>
<select>
<true>
<false>
<cmd_list>
<while>
<if>
<expressao>
<expressao1>
<elemento>
<relacional>
<relacional1>
<operador_relacional>
<aritimetica>
<aritimetica1>
<termo>
<termo1>
<fator1>
<fator>
#Grammar
<program>        ::= #15 <var_declare> "{" <comands> "}" #16 ;
<var_declare>  	::= î  | <type> #101 ":" <id_list>  #103 <var_declare>;
<id_list>       	::= id <id_list1> ;
<id_list1>	::= #102 "," id <id_list1> | #102 î;
<type>          	::= <simple> | <array> ;
<simple>        	::= number | literal | logical;
<array>         	::= array "["<simple>"," cnumerica"]" ;
<comands>       	::= <comand> <comands> | î ;
<comand>        	::= <var_attrib> | <read> | <write> | <select> | <while> ;
<var_attrib>    	::= id #102 <col> ":=" <expressao> #106 ;
<col>            	::= î | "[" <expressao> "]" ; 
<read>           	::= read "(" <id_read_list> #104 ")" ;
<id_read_list>  	::= id #102 <col> <id_read_list1> ;
<id_read_list1> ::=  "," id  #102  <col> <id_read_list1> | î ;
<write>          ::= write "(" <exp_list> ")" ;
<select>         ::= "(" <expressao> #107 ")" <true> <false> #108 ;
<true>          	::= ifTrueDo "{" <cmd_list> "}" ;
<false>         	::= î | #109 ifFalseDo "{" <cmd_list> "}" ;
<exp_list>      	::= <expressao>  <exp_list1> ;
<exp_list1>	::= #14 "," <expressao> <exp_list1> | #14 î;
<cmd_list>  	::= <comands> ;
<while>          ::= #110 do "{" <cmd_list> <if> <cmd_list> "}" #112 ;
<if>            	::= if "(" <expressao> #111 ")" exit ;
<expressao>	::= <elemento> <expressao1> ;
<expressao1>	::= î | and <elemento> #17 <expressao1> | or <elemento> #18 <expressao1>;
<elemento>	::= <relacional> | true #11 | false #12 | not <elemento> #13;
<relacional> 	::= <aritimetica> <relacional1> ;
<relacional1> 	::= <operador_relacional> #9 <aritimetica> #10 | î ;
<operador_relacional> ::= "=" | "!=" | "<" | "<=" | ">" | ">=" ;
<aritimetica> 	::=  <termo> <aritimetica1> ;
<aritimetica1>	::= î | "+" <termo> #1 <aritimetica1> | "-" <termo> #2 <aritimetica1> ;
<termo> 		::= <fator> <termo1> ;
<termo1>	::= î | "*" <fator> #3 <termo1> | "/" <fator> #4 <termo1>;
<fator>		::= id #105 <fator1> | cnumerica #6 | cliteral #20 | ccaractere #19 | "(" <expressao> ")" | "+" <fator> #7 | "-" <fator> #8 ;
<fator1>	::= "[" <expressao> "]" | î  ;

